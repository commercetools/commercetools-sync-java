plugins {
    id 'org.ajoberstar.git-publish' version '3.0.0'
    id 'com.adarshr.test-logger' version '3.0.0'
    id 'org.ajoberstar.grgit' version '4.1.0'
    id "com.github.ben-manes.versions" version '0.38.0'
    id 'ru.vyarus.mkdocs' version '2.1.1'
    id "com.github.spotbugs" version "4.7.1"
    id 'com.diffplug.spotless' version '5.12.5'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

ext{
    commercetoolsJvmSdkVersion = '1.63.0'
    mockitoJunitJupiterVersion = '3.10.0'
    jupiterApiVersion = '5.7.0'
    assertjVersion = '3.19.0'
    pmdVersion = '6.14.0'
    jacocoVersion = '0.8.4'
    caffeineVersion = '2.8.5'
    nexusStagingPluginVersion = '0.22.0'
}
apply from: "$rootDir/gradle-scripts/repositories.gradle"
apply from: "$rootDir/gradle-scripts/plugins.gradle"
apply from: "$rootDir/gradle-scripts/extensions.gradle"
apply from: "$rootDir/gradle-scripts/project-info.gradle"
apply from: "$rootDir/gradle-scripts/package.gradle"
apply from: "$rootDir/gradle-scripts/java-compile.gradle"
apply from: "$rootDir/gradle-scripts/integration-tests.gradle"
apply from: "$rootDir/gradle-scripts/test.gradle"
apply from: "$rootDir/gradle-scripts/benchmark.gradle"
apply from: "$rootDir/gradle-scripts/test-logger.gradle"
apply from: "$rootDir/gradle-scripts/pmd.gradle"
apply from: "$rootDir/gradle-scripts/jacoco.gradle"
apply from: "$rootDir/gradle-scripts/spotbugs.gradle"
apply from: "$rootDir/gradle-scripts/maven-publish.gradle"
apply from: "$rootDir/gradle-scripts/nexus-staging.gradle"
apply from: "$rootDir/gradle-scripts/javadocs-publish.gradle"
apply from: "$rootDir/gradle-scripts/set-library-version.gradle"
apply from: "$rootDir/gradle-scripts/execution-order.gradle"
apply from: "$rootDir/gradle-scripts/mkdocs.gradle"
apply from: "$rootDir/gradle-scripts/spotless.gradle"

dependencies {
    implementation "com.commercetools.sdk.jvm.core:commercetools-models:${commercetoolsJvmSdkVersion}"
    implementation "com.commercetools.sdk.jvm.core:commercetools-java-client-ahc-2_5:${commercetoolsJvmSdkVersion}"
    implementation "com.commercetools.sdk.jvm.core:commercetools-convenience:${commercetoolsJvmSdkVersion}"
    implementation "com.github.ben-manes.caffeine:caffeine:${caffeineVersion}"
    constraints {
        implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.12.3") {
            because "previous versions are vulnerable to DoS"
        }
        implementation("io.netty:netty-codec-http:4.1.63.Final") {
            because "previous versions are vulnerable to Information Disclosure"
        }
        implementation("org.apache.httpcomponents:httpclient:4.5.13") {
            because "previous versions are vulnerable to Improper Input Validation"
        }
        implementation("commons-codec:commons-codec:1.15") {
            because "previous versions are vulnerable to Information Exposure"
        }
    }
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoJunitJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterApiVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterApiVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterApiVersion}"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${jupiterApiVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
}
