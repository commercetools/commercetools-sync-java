group = rootProject.group
version = rootProject.version


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = project.name
                description = project.description
                url = scmHttpsUrl
                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = 'commercetools'
                        name = 'commercetools Professional Services'
                        url = 'https://github.com/commercetools'
                        email = 'ps-dev@commercetools.com'
                    }
                }
                scm {
                    connection = "scm:git:$scmHttpsUrl"
                    developerConnection = "scm:git:$scmSshUrl"
                    url = "$scmProjectUrl"
                }
            }
        }
    }
}

signing {
    def signingKey = System.getenv("PGP_KEY")
    def signingPassword = System.getenv("PGP_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
