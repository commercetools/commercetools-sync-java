gitPublish {
    repoUri = 'git@github.com:commercetools/commercetools-sync-java.git'

    // NOTE:
    // 1) $GRGIT_USER environment variable must be set to actual github token
    //    see http://ajoberstar.org/grgit/docs/groovydoc/index.html?org/ajoberstar/grgit/auth/AuthConfig.html
    //    https://github.com/ajoberstar/grgit#usage
    // 2) to use GH token `repoUri` must have an https (not git or ssh) protocol

    branch = 'gh-pages'
    // Copies the output of the 'javadoc' task into the path value passed to the 'into' method.
    // More about it:
    // https://docs.gradle.org/current/javadoc/org/gradle/api/file/CopySpec.html#from-java.lang.Object-groovy.lang.Closure-
    // https://docs.gradle.org/current/javadoc/org/gradle/api/Project.html#files-java.lang.Object...-
    contents {
        from (javadoc) {
            into "v/$rootProject.version"
        }
    }

    // what to keep in the existing branch (include=keep)
    preserve {
        include 'v/**', 'benchmarks/**', 'doc/**'
        exclude 'v/test**', 'doc/test**', 'v/development-SNAPSHOT', 'doc/development-SNAPSHOT' // always remove documentation of test releases
    }

    // message used when committing changes
    // The .toString() is temporarily used for this issue: https://github.com/ajoberstar/gradle-git-publish/issues/50
    commitMessage = "Publish a new java doc for $rootProject.version version.".toString()
}

gitPublishPush {
    repoUri = 'git@github.com:commercetools/commercetools-sync-java.git'
}

gitPublishReset {
    doFirst {


        validateVersion('Documentation publishing could be performed only with a valid release version')
    }
}
